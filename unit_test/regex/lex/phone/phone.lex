
export inform = (打电话给)?(张三) => request(contact="$2") <0.2>;
export inform = (播放)?(周杰伦)的?(青花瓷) => request(artist="$2", title="$3", domain="music") <0.6>;


####################################
#author:zhouleilei                 #
#date:2018.8.28                    #
####################################

#include "../comm/digit.lex"

全角数字 = <全角两位数字>|<全角一百以内数字>;
全角两位数字 = <全角一百以内数字>{1,2};
全角一百以内数字 = 0|1|2|3|4|5|6|7|8|9;


拨打操作 = <联系>|<打>;
打		 = 打;
联系	 = 联系|呼叫|接通|拨打|打通|拨通;

打电话	 = (<拨打操作>|通)(一个|个)?<电话>|(通过|由|用)?<电话>联系;
电话	 = 电话|号码|手机;

我想	 = <我>?(现在)?<程度>?(想|要|想要|需要);
给我	 = <给><我>;
我的	 = <我>的?;
我		 = 我|俺|大爷|爷|咱|咱们|老娘|本小姐|本大小姐|我们|本人|朕|我的宝宝|宝宝;
给		 = 给|帮|为|替;
程度	 = 真|很|超|比较|好|非常|特别|有点|有一点|最|超级|无比|太|挺;
和		 = 和|跟|与;
是		 = 是|为;

联系人	 = <称呼>|<通讯录>;
称呼	 = 妈妈|爸爸|爷爷|奶奶|外公|外婆|姥姥|姥爷|哥哥|姐姐|弟弟|妹妹|大姨|小姨|叔叔|婶婶|同事|同学|家人|家里|家|公司|客户|老公|老婆|媳妇|女朋友|男朋友|男票|女票;
通讯录	 = 张三|李四|小红|小明|小李|小张|老王|火警|报警|急救|急救中心|(中国)?<运营商>(客服)?;
运营商	 = (联通|移动|电信)的?;

请求礼貌 = 那?请?你?(随便)?(快|马上)?(<给我>|<我想>)?;
结尾礼貌 = <疑问助词>|(<好不好><疑问助词>?);
疑问助词 = 吗|呢|啊|咯|呀|吧|么|了|的|啦;
好不好	 = 好不好|行不行|好不|行不|如何|好|行|怎么样;

###### 联系|呼叫|接通|拨打 + contact|number + 的电话?，联系张三
export inform = ^<请求礼貌>?<联系><我的>?(<联系人>:contact|<全角数字>{1,13}:number)的?<运营商>?<电话>?<结尾礼貌>?$ => request() <2.0>;

###### 给|和? + contact|number + 打电话，张三打电话
export inform = ^<请求礼貌>?(<给>|<和>)?<我的>?(<联系人>:contact|<全角数字>{1,13}:number)的?<运营商>?<打电话><结尾礼貌>?$ => request() <2.0>;

###### 打电话 + 给? + contact|number，打电话张三
export inform = ^<请求礼貌>?<打电话>(<给>|<和>)?<我的>?(<联系人>:contact|<全角数字>{1,13}:number)的?<运营商>?<结尾礼貌>?$ => request() <2.0>;

###### 打 + 电话? + 给 + contact|number，打给张三
export inform = ^<请求礼貌>?(打|拨打)(一个)?<电话>?<给><我的>?(<联系人>:contact|<全角数字>{1,13}:number)的?<运营商>?<结尾礼貌>?$ => request() <2.0>;

###### 打 + contact|number+ 的电话|的号码，打张三的号码
export inform = ^<请求礼貌>?<拨打操作><我的>?(<联系人>:contact|<全角数字>{1,13}:number)的?<运营商>?的?<电话><结尾礼貌>?$ => request() <2.0>;

###### contact|number + 给 + 他 + 打电话，张三我要给他打电话|110我要给这个号码打电话
export inform = ^<我的>?(<联系人>:contact|<全角数字>{1,13}:number)<请求礼貌>?(<给>|<和>)(他|这个号码|这个号)的?<运营商>?<打电话><结尾礼貌>?$ => request() <2.0>;

###### 打电话 + 号码 + 是? + number
export inform = ^<请求礼貌>?<打电话><电话><是>(<全角数字>{1,13})的?<电话>?<结尾礼貌>?$ => request(number = "$1") <2.0>;



###### 未指定最大重复次数
export inform = <拨打操作><全角数字>{1,}:number => request() < 2.0 >;

###### 未指定最小重复次数
export inform = <拨打操作><全角数字>{,13}:number => request() < 2.0 >;

###### 最大值最小值相同，没有逗号
export inform = <拨打操作><全角数字>{13}:number => request() < 2.0 >;

###### 方括号
export inform = <拨打操作>[1-9]{1, 13}:number => request() < 2.0 >;

###### . 通配符
export inform = 给我拨打(.)的电话 => request(contact = "$1") < 2.0 >;

###### 转义字符
export inform = 给\^我拨打<联系人>的电话 => request(contact = "$1") < 2.0 >;








