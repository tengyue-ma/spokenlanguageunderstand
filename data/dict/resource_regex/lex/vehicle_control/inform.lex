####################################
##                                ##
##        author:lengting         ##
##        time:2018-09-18         ##
##        version: v1.0           ##
##        modify:2018-12-10       ##
##                                ##
####################################


#include "../comm/digit.lex"


礼貌前缀	= 你|请|请你|麻烦|麻烦你|拜托|拜托你|能|你能|能不能|你能不能|能不能够|你能不能够|是否能|你是否能|能否|你能否|可以|你可以|可不可以|你可不可以|可以不可以|你可以不可以|是否可以|你是否可以|可否|你可否;
帮	= 帮|替|给;
我	= 我|人家|老子|老娘|本小姐|小爷我|大爷我;
要	= 要|想|想要|需要;
祈使开头	= <礼貌前缀>?<帮>?<我>?|<礼貌前缀>?帮?忙?|<我>?<要>?;
把	= 把|将;

顺序连词	= 先|首先|后|然后|之后|再|接着|紧接着;
并列连词	= 和|并|并且|以及;
连词	= <顺序连词>|<并列连词>;

句末语气词	= 呗|吧|了|了吧|吗|么;
可否	= 好吧|好吗|好么|好嘛|好不|好不好|行吗|行么|行嘛|行不|行不行|可以吗|可以么|可以嘛|可不可以|可以不|如何;
结尾请求	= <句末语气词>|<可否>|谢谢;

直接	= 直接|一直|一下子;
快	= 快|赶紧|赶快|迅速|准备;

打开前置	= <快>?(开|打开|开启|开开|开始)(一?下)?;
关闭前置	= <快>?(关|关闭|关掉|关上|关关|关了)(一?下)?;
都	= 也?(都|全|全都|全部|全部都|同时|一起);
打开后置	= <礼貌前缀>?<帮>?<我>?<都>?<打开前置>;
关闭后置	= <礼貌前缀>?<帮>?<我>?<都>?(关起来|<关闭前置>);
切换到	= (切|换|切换|转换|调|调节|调整|设|设置|开|打开|开启|进入|定|设定|改|更改|修改)(到|至|成|为);
切换一下	= (换|切换|转换|调|调节|调整|设|设置|开|打开|开启|进入|定|设定|打|改|更改|修改)(一?下)?;
到	= 到|至|成|为;
升	= 升|加|增|提;
调高	= <升>|调高|调大|开高|开大|升高|增加|提高|提升|加大|加高;
降	= 降|减|弱;
调低	= <降>|调低|调小|开低|开小|关低|关小|降低|减少|减小|减弱|调弱;
一点	= 一?点点?|一?些些?|一下下?|几档|几度|几摄氏度;
高低	= 高|大|强|低|小|弱;
升降	= <升>|<降>|增加|提升|减少|减弱;
程度修饰	= 再|继续|还需|还要|还需要|稍微|稍稍;

那个	= 那个|这个;
全部	= 全部|所有;
两三个	= 两个|三个;
两边	= 两边|两侧;
和	= 和|与|跟|以及|还有;

侧面	= 侧|侧面|左侧|右侧|后侧|侧后方?|斜后方?;
旁	= 旁|旁边|隔壁|边上|这边|那边|<侧面>;
座	= 座|席;
驾驶位	= 驾驶<座>?位?置?<旁>?的?|主驾驶?<座>?位?置?<旁>?的?|副驾驶?<座>?位?置?<旁>?的?|主副驾驶?<座>?位?置?<旁>?的?|主座位?置?<旁>?的?|副座位?置?<旁>?的?;
第N排	= 第一排|第二排|第三排|前一排|前两排|前三排|最?后一排|最?后两排|最?后三排;
左右	= (左前|右前|左后|右后)(方|侧|面)?的?;
顶部	= (全景|车顶|顶部|头顶|头上|顶上|上方|上面|头顶上|头上方|头上面|头顶上方|头顶上面)的?;
前后左右	= (前|后|左|右)(面|边|方|侧|排|座)?的?;
单个车窗描述	= 靠?近?(<驾驶位>|<左右>|<顶部>|<前后左右><前后左右>?|<第N排>的?<侧>?的?);
排	= (前|后|前后)(排|面|边);
侧	= (左|右|左右)(侧|面|边);
多个车窗描述 = (<排>|<侧>|<前后左右><和><前后左右>|<全部>|<驾驶位><和><驾驶位>|<两边>|<前后左右><前后左右><两边>|整个?车子?)的?;

车窗	= 窗|窗子|车窗|窗户;

天窗	= <顶部>?<那个>?天窗|<顶部><那个>?<车窗>;
车窗对象	= <天窗>|<车窗>|<单个车窗描述><那个>?<车窗>|<多个车窗描述><全部>?的?<车窗>|<多个车窗描述><两三个>?<车窗>;

循环	= 内循环|外循环|内外循环|内循环外循环|外循环内循环;
循环系统对象	= 循环系统;

模式	= 制冷|制热|出风;
风向	= 除霜|吹面|吹头|吹脸|吹脚|吹面<和>?吹脚|吹头<和>?吹脚|吹脸<和>?吹脚|除霜<和>?吹脚|吹脚<和>?吹面|吹脚<和>?吹头|吹脚<和>?吹脸|吹脚<和>?除霜;
风力	= (风|风力|风量|出风量|风速)值?;
档位	= 档|档位|级|级别;
最小	= 最小|最弱|最低;
最大	= 最大|最强|最高;
度	= 度|摄氏度;
空调对象	= (全部)?空调;

车内	= 车子?内|车子?內|车子?里面?|车子?里边|全车|车|车身|车子?上;
对象	= <车窗对象>|<循环系统对象>|<空调对象>;




##############################################
## 打开 [对象]
export inform = ^<连词>?<祈使开头><打开前置><我>?的?<车内>?的?(<对象>:obj_1)<结尾请求>?$ => request(opr_1="open") <5.0>;
export inform = ^<连词>?<祈使开头>(降|下降)一?下?<我>?的?<车内>?的?(<车窗对象>:obj_1)<结尾请求>? => request(opr_1="open") <5.0>;
export inform = ^<连词>?<礼貌前缀>?<我>?<要>?吹(空调:obj_1)<结尾请求>? => request(opr_1="open") <5.0>;
## 把字句: 把 [对象] 打开
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?(<对象>:obj_1)<打开后置><结尾请求>?$ => request(opr_1="open") <5.0>;
## 关闭 [对象]
export inform = ^<连词>?<祈使开头><关闭前置><我>?的?<车内>?的?(<对象>:obj_1)<结尾请求>?$ => request(opr_1="close") <5.0>;
export inform = ^<连词>?<祈使开头>(升起|升上|合起|合上)<我>?的?<车内>?的?(<车窗对象>:obj_1)<结尾请求>?$ => request(opr_1="close") <5.0>;
## 把字句: 把 [对象] 关闭
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?(<对象>:obj_1)<关闭后置><结尾请求>?$ => request(opr_1="close") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?(<车窗对象>:obj_1)<都>?(升起|升上|合起|合上)来?<结尾请求>?$ => request(opr_1="close") <5.0>;

## "关闭车窗" = "把车窗关上" = "把车窗全都关掉", 返回结果相同, 均为"obj_1=车窗, opr_1=close"
## "关闭所有的车窗" = "把所有的车窗关上" = "把所有的车窗全部都关闭", 返回结果相同, 均为"obj_1=所有的车窗, opr_1=close"
## "把车窗全部关掉" 与 "关闭全部车窗" 的不同点在于, 前者"全部"后置修饰"车窗", 后者是"全部"正常语序修饰"车窗", 返回不同的结果 "车窗" 与 "全部车窗"
## 打开操作类似


##############################################
## 打开 [内外循环]  或  切换到 [内外循环]
export inform = ^<连词>?<祈使开头>(<打开前置>|<切换一下><到>)?(<循环>:cycle_val_1)的?(模式|系统)?<结尾请求>?$ => request(obj_1="循环系统") <5.0>;
## 把字句: 把 [内外循环] 打开
export inform = ^<连词>?<祈使开头><把>?(<循环>:cycle_val_1)的?(模式|系统)?<打开后置><结尾请求>?$ => request(obj_1="循环系统") <5.0>;
## 关闭 [内外循环]
export inform = ^<连词>?<祈使开头><关闭前置>(<循环>:cycle_val_1)的?(模式|系统)?<结尾请求>?$ => request(obj_1="循环系统", opr_1="close") <5.0>;
## 把字句: 把 [内外循环] 关闭
export inform = ^<连词>?<祈使开头><把>?(<循环>:cycle_val_1)的?(模式|系统)?<关闭后置><结尾请求>?$ => request(obj_1="循环系统", opr_1="close") <5.0>;


##############################################
## 风向控制
## "模式"出现与否: [吹面]  或  [吹面] 模式
## "空调"出现与否: [吹面]  或  空调 [吹面]
## "打开"与"切换"出现与否: [吹面]  或  打开[吹面]  或  切换到[吹面]模式
## "打开"/"切换"与"空调"的位置: 打开空调[吹面]模式  或  调整空调到[吹面]模式  或  空调打开[吹面]模式  或  把空调切换到[吹面]模式
export inform = ^<连词>?<祈使开头>(<打开前置>|<切换到>|<切换一下>)?(空调)?的?(风|风向|模式)?到?(<风向>:wd_dir_val_1)(模式|功能)?<结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头><把>?(空调)?的?(风|风向|模式)?(<帮><我>)?(<打开前置>到?|<切换到>|<切换一下>)(<风向>:wd_dir_val_1)(模式|功能)?<结尾请求>?$ => request(obj_1="空调") <5.0>;


##############################################
## 风力控制
## 最小风力
## "风力"与"最小"的位置: [风力] [最小]  或  [最小] [风力]
## "档/档位"出现与否: [风力] [最小] 档  或  [最小] 档 [风力]
## "档/档位"在"风力"后出现与否: [风力] 档位 [最小]  或  [最小] [风力] 档位
## "调节"出现与位置: [风力] [调节] 到 [最小] 档    =>    "把"前缀
##                   [调节] 到 [最小] 档 [风力]
##                   [调节] [风力] 到 [最小] 档
## "空调"出现与位置: [空调] [风力] [调节] 到 [最小] 档    =>    "把"前缀
##                   [空调] [调节] 到 [最小] 档 [风力]    =>    "把"前缀
##                   [调节] [空调] [风力] 到 [最小] 档
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?<风力>的?<档位>?(<帮><我>)?<直接>?(<切换一下>|<调低>)?<到>?<最小>的?<档位>?<结尾请求>?$ => request(obj_1="空调", wd_pwr_val_1="min") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?(<帮><我>)?<直接>?(<切换一下>|<调低>)?<到>?<最小>的?<档位>?的?<风力>的?<档位>?<结尾请求>?$ => request(obj_1="空调", wd_pwr_val_1="min") <5.0>;
export inform = ^<连词>?<祈使开头>(<切换一下>|<调低>)?<我>?的?<车内>?的?<空调对象>?的?<风力>的?<档位>?<到>?<最小>的?<档位>?<结尾请求>?$ => request(obj_1="空调", wd_pwr_val_1="min") <5.0>;
## 最大风力
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?<风力>的?<档位>?(<帮><我>)?<直接>?(<切换一下>|<调高>)?<到>?<最大>的?<档位>?<结尾请求>?$ => request(obj_1="空调", wd_pwr_val_1="max") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?(<帮><我>)?<直接>?(<切换一下>|<调高>)?<到>?<最大>的?<档位>?的?<风力>的?<档位>?<结尾请求>?$ => request(obj_1="空调", wd_pwr_val_1="max") <5.0>;
export inform = ^<连词>?<祈使开头>(<切换一下>|<调高>)?<我>?的?<车内>?的?<空调对象>?的?<风力>的?<档位>?<到>?<最大>的?<档位>?<结尾请求>?$ => request(obj_1="空调", wd_pwr_val_1="max") <5.0>;
## 调至具体风力值
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?<风力>的?<档位>?(<帮><我>)?(<切换一下><到>|<调高><到>|<调低><到>|设置|设定)?第?(<一百以内数字>:wd_pwr_abs_val_1)<档位><结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?(<帮><我>)?(<切换一下><到>|<调高><到>|<调低><到>|设置|设定)?第?(<一百以内数字>:wd_pwr_abs_val_1)<档位>的?<风力><结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头>(<切换一下>|<调高>|<调低>)<我>?的?<车内>?的?<空调对象>?的?<风力>的?<档位>?<到>第?(<一百以内数字>:wd_pwr_abs_val_1)<档位><结尾请求>?$ => request(obj_1="空调") <5.0>;
## 调高/低N档风力值
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?<风力>的?<档位>?<程度修饰>?(调|调节|调整|设|设置|开|<升降>)?(<高低>:wd_pwr_rel_sign_1)(<一百以内数字>:wd_pwr_rel_val_1)个?<档位><结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?<程度修饰>?(调|调节|调整|设|设置|开|<升降>)(<高低>:wd_pwr_rel_sign_1)(<一百以内数字>:wd_pwr_rel_val_1)个?<档位>的?<风力><结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头><程度修饰>?(调|调节|调整|设|设置|开|<升降>)(<高低>:wd_pwr_rel_sign_1)<我>?的?<车内>?的?<空调对象>?的?<风力>(<一百以内数字>:wd_pwr_rel_val_1)个?<档位><结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?<风力>的?<档位>?<程度修饰>?(<升降>:wd_pwr_rel_sign_1)(<一百以内数字>:wd_pwr_rel_val_1)个?<档位><结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?<程度修饰>?(<升降>:wd_pwr_rel_sign_1)(<一百以内数字>:wd_pwr_rel_val_1)个?<档位>的?<风力><结尾请求>?$ => request(obj_1="空调") <5.0>;
## 风力调高一点
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?<风力>的?<档位>?(<帮><我>)?<程度修饰>?(<调高>|<切换一下>?高|<切换一下>?大<一点>)<一点>?<结尾请求>?$ => request(obj_1="空调", wd_pwr_step_val_1="step") <5.0>;
export inform = ^<连词>?<祈使开头><程度修饰>?(<调高>|<切换一下>?高|<切换一下>?大)<一点>?<我>?的?<车内>?的?<空调对象>?的?<风力>的?<档位>?<结尾请求>?$ => request(obj_1="空调", wd_pwr_step_val_1="step") <5.0>;
export inform = ^<我>?的?<车内>?的?<空调对象>?的?<风力>的?<档位>?(太|有一?点|有一?些)(小|弱)了?$ => request(obj_1="空调", wd_pwr_step_val_1="step") <5.0>;
## 风力调小一点
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?<风力>的?<档位>?(<帮><我>)?<程度修饰>?(<调低>|<切换一下>?低|<切换一下>?小<一点>)<一点>?<结尾请求>?$ => request(obj_1="空调", wd_pwr_step_val_1="-step") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>的?<风力>?的?<档位>?(<帮><我>)?<程度修饰>?<切换一下>?小<一点>?<结尾请求>?$ => request(obj_1="空调", wd_pwr_step_val_1="-step") <5.1>;
export inform = ^<连词>?<祈使开头><程度修饰>?(<调低>|<切换一下>?低|<切换一下>?小)<一点>?<我>?的?<车内>?的?<空调对象>?的?<风力>的?<档位>?<结尾请求>?$ => request(obj_1="空调", wd_pwr_step_val_1="-step") <5.0>;
export inform = ^<我>?的?<车内>?的?<空调对象>?的?<风力>的?<档位>?(太|有一?点|有一?些)(大|强)了?$ => request(obj_1="空调", wd_pwr_step_val_1="-step") <5.0>;


##############################################
## 空调模式控制
## [制冷] 模式  或  打开 [制冷] 模式  或  切换到 [制冷] 模式
export inform = ^<连词>?<祈使开头>(<打开前置>|<切换到>|<切换一下>)?<我>?的?<车内>?的?<空调对象>?的?(模式|功能|温度)?<到>?(<模式>:mode_val_1)的?(模式)?<结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头><打开前置><我>?的?<车内>?的?(压缩机:mode_val_1)<结尾请求>?$ => request(obj_1="空调") <5.0>;
## 把字句: 把 空调 切换到 [制冷] 模式
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>的?(模式|功能|温度)?(<帮><我>)?(<打开前置>|<切换一下>)<到>?(<模式>:mode_val_1)的?(模式)?<结尾请求>?$ => request(obj_1="空调") <5.0>;
## 把字句: 把 [制冷] 模式 打开
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)的?(模式|功能)?<打开后置><结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?(压缩机:mode_val_1)<打开后置><结尾请求>?$ => request(obj_1="空调") <5.0>;
## 关闭 [制冷] 模式
export inform = ^<连词>?<祈使开头><关闭前置><我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)的?(模式|功能)?<结尾请求>?$ => request(obj_1="空调", opr_1="close") <5.0>;
export inform = ^<连词>?<祈使开头><关闭前置><我>?的?<车内>?的?(压缩机:mode_val_1)<结尾请求>?$ => request(obj_1="空调", opr_1="close") <5.0>;
## 把字句: 把 [制冷] 模式 关闭
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)的?(模式|功能)?<关闭后置><结尾请求>?$ => request(obj_1="空调", opr_1="close") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?(压缩机:mode_val_1)<关闭后置><结尾请求>?$ => request(obj_1="空调", opr_1="close") <5.0>;


##############################################
## 温度设置
## 最低温度
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式|功能)?的?温度的?值?(<帮><我>)?<直接>?<打开前置>?(<切换一下>|<调低>)<到>最(低|小)的?(温度|摄氏度)?的?值?<结尾请求>?$ => request(obj_1="空调", temp_val_1="min") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式|功能)?(<帮><我>)?<打开前置>?<直接>?(<切换一下>|<调低>)<到>最(低|小)的?(温度|摄氏度)的?值?<结尾请求>?$ => request(obj_1="空调", temp_val_1="min") <5.0>;
export inform = ^<连词>?<祈使开头>(<切换一下>|<调低>)<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式|功能)?的?温度的?值?<到>最(低|小)的?(温度|摄氏度)?的?值?<结尾请求>?$ => request(obj_1="空调", temp_val_1="min") <5.0>;
export inform = ^<连词>?<祈使开头>(<切换一下>|<调低>)<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式|功能)?<到>最(低|小)的?(温度|摄氏度)的?值?<结尾请求>?$ => request(obj_1="空调", temp_val_1="min") <5.0>;
## 最高温度
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式|功能)?的?温度的?值?(<帮><我>)?<打开前置>?<直接>?(<切换一下>|<调高>)<到>最(高|大)的?(温度|摄氏度)?的?值?<结尾请求>?$ => request(obj_1="空调", temp_val_1="max") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式|功能)?(<帮><我>)?<打开前置>?<直接>?(<切换一下>|<调高>)<到>最(高|大)的?(温度|摄氏度)的?值?<结尾请求>?$ => request(obj_1="空调", temp_val_1="max") <5.0>;
export inform = ^<连词>?<祈使开头>(<切换一下>|<调高>)<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式|功能)?的?温度的?值?<到>最(高|大)的?(温度|摄氏度)?的?值?<结尾请求>?$ => request(obj_1="空调", temp_val_1="max") <5.0>;
export inform = ^<连词>?<祈使开头>(<切换一下>|<调高>)<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式|功能)?<到>最(高|大)的?(温度|摄氏度)的?值?<结尾请求>?$ => request(obj_1="空调", temp_val_1="max") <5.0>;
## 调至具体温度值
export inform = ^<连词>?<祈使开头><把>?<打开前置>?<我>?的?<车内>?的?<空调对象>的?(<模式>:mode_val_1)?的?(模式|功能)?的?(温度)?的?值?(<帮><我>)?<打开前置>?(<切换一下>?<到>|<调高><到>|<调低><到>|设置|设定)?(<一百以内数字>:temp_abs_val_1)<度><结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头>(<切换一下>|<调高>|<调低>)<我>?的?<车内>?的?<空调对象>的?(<模式>:mode_val_1)?的?(模式|功能)?的?(温度)?的?值?<到>(<一百以内数字>:temp_abs_val_1)<度><结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<打开前置>?<我>?的?<车内>?的?<空调对象>的?(温度)?的?值?(<帮><我>)?(<切换一下>?<到>|<调高><到>|<调低><到>|设置|设定)?(<模式>:mode_val_1)?的?(模式|功能)?的?(<一百以内数字>:temp_abs_val_1)<度><结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式|功能)?的?(温度)的?值?(<帮><我>)?(<切换一下>?<到>|<调高><到>|<调低><到>|设置|设定)?(<一百以内数字>:temp_abs_val_1)<度><结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头>(<切换一下>|<调高>|<调低>)<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式|功能)?的?(温度)的?值?<到>(<一百以内数字>:temp_abs_val_1)<度><结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?(温度)的?值?(<帮><我>)?(<切换一下>?<到>|<调高><到>|<调低><到>|设置|设定)?(<模式>:mode_val_1)?的?(模式|功能)?的?(<一百以内数字>:temp_abs_val_1)<度><结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式|功能)?的?温度的?值?(<帮><我>)?(<切换一下><到>|<调高><到>|<调低><到>|设置|设定)(<一百以内数字>:temp_abs_val_1)<结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头>(<切换一下>|<调高>|<调低>)<我>?的?<车内>?的?<空调对象>的?(<模式>:mode_val_1)?的?(模式|功能)?的?温度的?值?<到>(<一百以内数字>:temp_abs_val_1)<结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?(<模式>:mode_val_1)的?(模式|功能)?的?(温度)?的?值?(<帮><我>)?<打开前置>?(<切换一下>?<到>|<调高><到>|<调低><到>|设置|设定)(<一百以内数字>:temp_abs_val_1)<度><结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头>(<帮><我>)?(<切换一下><到>|<调高><到>|<调低><到>|设置|设定)(<模式>:mode_val_1)的?(模式|功能)?的?(<一百以内数字>:temp_abs_val_1)<度><结尾请求>?$ => request(obj_1="空调") <5.0>;
## 调高/低N度
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式|功能)?的?温度的?值?(<帮><我>)?<程度修饰>?(调|调节|调整|设|设置|开|<升降>)?(<高低>:temp_rel_sign_1)(<一百以内数字>:temp_rel_val_1)<度><结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式|功能)?的?温度的?值?(<帮><我>)?<程度修饰>?(<升降>:temp_rel_sign_1)(<一百以内数字>:temp_rel_val_1)<度><结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>的?(<模式>:mode_val_1)?的?(模式|功能)?(<帮><我>)?<程度修饰>?(调|调节|调整|设|设置|开|<升降>)(<高低>:temp_rel_sign_1)(<一百以内数字>:temp_rel_val_1)<度><结尾请求>?$ => request(obj_1="空调") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>的?(<模式>:mode_val_1)?的?(模式|功能)?(<帮><我>)?<程度修饰>?(<升降>:temp_rel_sign_1)(<一百以内数字>:temp_rel_val_1)<度><结尾请求>?$ => request(obj_1="空调") <5.0>;
## 温度调高一点
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>的?(<模式>:mode_val_1)?的?(模式|功能)?的?(温度)?的?值?(<帮><我>)?<程度修饰>?(<调高>|<切换一下>?高)<一点>?<结尾请求>?$ => request(obj_1="空调", temp_step_val_1="step") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式|功能)?的?温度的?值?(<帮><我>)?<程度修饰>?(<调高>|<切换一下>?高)<一点>?<结尾请求>?$ => request(obj_1="空调", temp_step_val_1="step") <5.0>;
export inform = ^<连词>?<祈使开头><调高><一点>?<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式|功能)?的?温度的?值?<结尾请求>?$ => request(obj_1="空调", temp_step_val_1="step") <5.0>;
export inform = ^<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式)?的?温度(太|有一?点|有一?些)(低|凉|冷)了?$ => request(obj_1="空调", temp_step_val_1="step") <5.0>;
export inform = ^(<我>觉得|<我>感觉|感觉|我看|现在)?(现在)?<我>?的?<车内>(实在|确实)?是?(太|好|实在|确实|有一?点|有一?些)?(冷冷?|凉凉?|好冷好冷|好凉好凉|太冷太冷|太凉太凉)(了|啊|啦|的|哦|噢|耶|唉)?$ => request(obj_1="空调", temp_step_val_1="step") <5.0>;
## 温度调低一点
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>的?(<模式>:mode_val_1)?的?(模式|功能)?的?(温度)?的?值?(<帮><我>)?<程度修饰>?(<调低>|<切换一下>?低)<一点>?<结尾请求>?$ => request(obj_1="空调", temp_step_val_1="-step") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式|功能)?的?温度的?值?(<帮><我>)?<程度修饰>?(<调低>|<切换一下>?低)<一点>?<结尾请求>?$ => request(obj_1="空调", temp_step_val_1="-step") <5.0>;
export inform = ^<连词>?<祈使开头><调低><一点>?<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式|功能)?的?温度的?值?<结尾请求>?$ => request(obj_1="空调", temp_step_val_1="-step") <5.0>;
export inform = ^<我>?的?<车内>?的?<空调对象>?的?(<模式>:mode_val_1)?的?(模式)?的?温度(太|有一?点|有一?些)(高|热)了?$ => request(obj_1="空调", temp_step_val_1="-step") <5.0>;
export inform = ^(<我>觉得|<我>感觉|感觉|我看|现在)?(现在)?<我>?的?<车内>(实在|确实)?是?(太|好|实在|确实|有一?点|有一?些)?(热热?|好热好热|太热太热)(了|啊|啦|的|哦|噢|耶|唉)?$ => request(obj_1="空调", temp_step_val_1="-step") <5.0>;



##############################################
##############################################
## 打开与关闭多操作
export inform = ^<连词>?<祈使开头><打开前置><我>?的?<车内>?的?(<对象>:obj_1)<和><我>?的?<车内>?的?(<对象>:obj_2)<结尾请求>?$ => request(opr_1="open", opr_2="open") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?(<对象>:obj_1)<和><我>?的?<车内>?的?(<对象>:obj_2)<打开后置><结尾请求>?$ => request(opr_1="open", opr_2="open") <5.0>;
export inform = ^<连词>?<祈使开头><关闭前置><我>?的?<车内>?的?(<对象>:obj_1)<和><我>?的?<车内>?的?(<对象>:obj_2)<结尾请求>?$ => request(opr_1="close", opr_2="close") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?(<对象>:obj_1)<和><我>?的?<车内>?的?(<对象>:obj_2)<关闭后置><结尾请求>?$ => request(opr_1="close", opr_2="close") <5.0>;

export inform = ^<连词>?<祈使开头><打开前置><我>?的?<车内>?的?(<对象>:obj_1)<和><我>?的?<车内>?的?(<循环>:cycle_val_2)的?(模式|系统)?<结尾请求>?$ => request(opr_1="open", obj_2="循环系统") <5.0>;
export inform = ^<连词>?<祈使开头><打开前置><我>?的?<车内>?的?(<循环>:cycle_val_1)的?(模式|系统)?<和><我>?的?<车内>?的?(<对象>:obj_2)<结尾请求>?$ => request(obj_1="循环系统", opr_2="open") <5.0>;
export inform = ^<连词>?<祈使开头><打开前置><我>?的?<车内>?的?(<循环>:cycle_val_1)的?(模式|系统)?<和><我>?的?<车内>?的?(<循环>:cycle_val_2)的?(模式|系统)?<结尾请求>?$ => request(obj_1="循环系统", obj_2="循环系统") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?(<对象>:obj_1)<和><我>?的?<车内>?的?(<循环>:cycle_val_1)的?(模式|系统)?<打开后置><结尾请求>?$ => request(opr_1="open", obj_2="循环系统") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?(<循环>:cycle_val_1)的?(模式|系统)?<和><我>?的?<车内>?的?(<对象>:obj_2)<打开后置><结尾请求>?$ => request(obj_1="循环系统", opr_2="open") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?(<循环>:cycle_val_1)的?(模式|系统)?<和><我>?的?<车内>?的?(<循环>:cycle_val_2)的?(模式|系统)?<打开后置><结尾请求>?$ => request(obj_1="循环系统", obj_2="循环系统") <5.0>;

export inform = ^<连词>?<祈使开头><关闭前置><我>?的?<车内>?的?(<对象>:obj_1)<和><我>?的?<车内>?的?(<循环>:cycle_val_2)的?(模式|系统)?<结尾请求>?$ => request(opr_1="close", obj_2="循环系统", opr_2="close") <5.0>;
export inform = ^<连词>?<祈使开头><关闭前置><我>?的?<车内>?的?(<循环>:cycle_val_1)的?(模式|系统)?<和><我>?的?<车内>?的?(<对象>:obj_2)<结尾请求>?$ => request(obj_1="循环系统", opr_1="close",  opr_2="close") <5.0>;
export inform = ^<连词>?<祈使开头><关闭前置><我>?的?<车内>?的?(<循环>:cycle_val_1)的?(模式|系统)?<和><我>?的?<车内>?的?(<循环>:cycle_val_2)的?(模式|系统)?<结尾请求>?$ => request(obj_1="循环系统", opr_1="close", obj_2="循环系统", opr_2="close") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?(<对象>:obj_1)<和><我>?的?<车内>?的?(<循环>:cycle_val_1)的?(模式|系统)?<关闭后置><结尾请求>?$ => request(opr_1="close", obj_2="循环系统", opr_2="close") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?(<循环>:cycle_val_1)的?(模式|系统)?<和><我>?的?<车内>?的?(<对象>:obj_2)<关闭后置><结尾请求>?$ => request(obj_1="循环系统", opr_1="close", opr_2="close") <5.0>;
export inform = ^<连词>?<祈使开头><把>?<我>?的?<车内>?的?(<循环>:cycle_val_1)的?(模式|系统)?<和><我>?的?<车内>?的?(<循环>:cycle_val_2)的?(模式|系统)?<关闭后置><结尾请求>?$ => request(obj_1="循环系统", opr_1="close", obj_2="循环系统", opr_2="close") <5.0>;




#############################################
#############################################
## 针对性case: 空调温度最高 / 空调温度最低 / 空调风力最大 / 空调风力最小
export inform = 空调温度最高 => request(obj_1="空调", temp_val_1="max") <5.0>;
export inform = 空调温度最低 => request(obj_1="空调", temp_val_1="min") <5.0>;

