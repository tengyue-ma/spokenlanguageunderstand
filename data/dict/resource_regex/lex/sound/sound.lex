#########################
#author:zhouleilei      #
#date:2018.4.28         #
#########################

#include "../comm/digit.lex"

播放操作 = (<我想听>|<你会>|<你有>|<给我播放>)<一段>?;

我想听	 = <我>?<想>?<播放>;
你会	 = (你|你再|你会|你听过|你会不会|会不会)<播放>?;
你有	 = 你?<有没有>;
给我播放 = <请求礼貌>?<给我>?<播放>;
给我	 = 来?(给|帮|为)<我>;

我		 = 我|俺|大爷|爷|咱|咱们|老娘|本小姐|本大小姐|我们|本人|朕|我的宝宝|宝宝;
想		 = <程度>?(想|要|想要);
程度	 = 好|非常|特别;
播放	 = 播|播放|放|唱|唱唱|听|听听|点播|学|来|来听;
有没有	 = 这有|这里有|有没有|有木有|有么有|是否有|还有|有;
一段	 = 个|段|一段|一个|几段|一些|一下|下|声;

你知道	 = 你知道|你知不知道;
请求礼貌 = <能不能>?请?(快|马上)?;
能不能	 = 能不能|可以|可不可以;

结尾礼貌 = <疑问助词>|(<好不好><疑问助词>?);
疑问助词 = 吗|呢|啊|咯|呀|吧|么|了|的;
好不好	 = 好不好|行不行|好不|行不|如何|可以|好|行|怎么样|可不可以;

形容词	 = (真|很|超)?(好听|不错|棒|可爱|动听);
声音	 = 声音|声|音效|音;
叫		 = 叫|吼|吠;
叫声	 = 叫声|吼声|咆哮;

是什么	 = 是(什么|什么样|怎样|怎么样|哪种);
是怎么	 = 是?(怎么|怎样|怎么样);
学习	 = (<我想听>你?|<给我>|<你会>)?再?(学|学习|表演|模仿)<一段>?;

动作	 = 吹|弹|打|拉|弹拨|吹奏|弹奏;

###### 自然
###### 流水声|播放流水的声音|流水是什么样的声音
export sound00 = ^<播放操作>?(&<sound_nature>)<是什么>?的?<声音><形容词>?<播放操作>?<结尾礼貌>?$ => request(sounder="$1", intent="sound") <1.0>;

###### 动物
###### 狗的声音|狗叫的声音|狗的叫声|狗叫是什么声音|狗声真好听你会吗
###### [给我]学个狗叫|学个狗叫的声音
###### 狗怎么叫|你知道狗怎么叫吗
export sound10 = ^<播放操作>?(&<sound_animal>)<叫>?<是什么>?的?(<声音>|<叫声>|<叫>)<形容词>?<播放操作>?<结尾礼貌>?$ => request(sounder="$1", intent="sound") <1.0>;
export sound11 = ^<学习>(&<sound_animal>)<叫>?的?(<声音>|<叫声>|<叫>)<播放操作>?<结尾礼貌>?$ => request(sounder="$1", intent="sound") <1.0>;
export sound11 = ^<你知道>?(&<sound_animal>)<是怎么><叫>的?<结尾礼貌>?$ => request(sounder="$1", intent="sound") <1.0>;

###### 乐器
###### 我想听弹拨乐器的声音
export sound20 = ^<播放操作>?<动作>?(&<sound_instrument>)<是什么>?的?<声音><形容词>?<播放操作>?<结尾礼貌>?$ => request(sounder="$1", intent="sound") <1.0>;

###### 其他
###### 放屁的声音|放屁声
###### 你会放屁吗|放个屁
export sound30 = ^<播放操作>?<播放操作>?(屁)<是什么>?的?<声音><形容词>?<播放操作>?<结尾礼貌>?$ => request(sounder="$1", intent="sound") <1.0>;
export sound31 = ^<播放操作>(屁)的?<声音>?<播放操作>?<结尾礼貌>?$ => request(sounder="$1", intent="sound") <1.0>;





