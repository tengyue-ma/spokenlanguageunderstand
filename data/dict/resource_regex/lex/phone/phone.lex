####################################
#    author : liushuhuan           #
#    create : 2019.1.3             #
#    描  述  : 离线版本V0.1          #
####################################

人称代词 = 我|我们|俺|咱们|咱|大爷我|本小姐|本小姐我|宝宝|本宝宝;
助动词 = 想|要|想要|需要|得|准备;

## 将"前往电话"和"打开电话"归为control, 所以删除"前往"和"打开"
拨打 = 打|拨打|拨|拨通;
接 = 接;
接听 = 接听|接通;
挂 = 挂;
挂断 = 挂断|挂掉|挂了;
数量介词 = 个|一个|下|一下;
指代词 = 这个|这|这一个|这通|这一通;
电话 = 电话|手机|号码;

## 参考submusic
语气词 = 吧|吗|嘛|啊|呀|么|了|啦|呢|哇;
语气助词 = ((好|行|可以)<语气词>)|好不|好不好|行不|行不行|可不可以|可以不|如何|怎么样|谢谢;
结尾词 = <语气词>|(<语气助词><语气词>?);

祈使语气词 = (能不能|能|可以|可不可以|拜托|请|请你|拜托你|让你){1,2};

##
帮助 = 帮|替|帮忙|帮助|代;
把 = 把;
问候语 = 你好<语气词>?;


############ inform ############
############ 发起打电话的命令: 打电话/我想打电话/我要打电话/帮我打个电话/
############ 暂不支持: 打|拨打|拨|拨通
###### 人称代词 + 助动词 + 动词 + 数量词 + 名词 + 语气词, <我><想><打><一个><电话>
## 举例: 打电话/我想打电话/我要打电话/我想打电话可以吗
export inform = ^<问候语>?(<人称代词><助动词>?)?<拨打><数量介词>?<电话><结尾词>?$ => request(intent="inform") <3.0>;

###### 助动词 + 人称代词 + 动词 + 数量词 + 名词 + 语气词, <帮><我><打><一个><电话>
## 举例: 帮我打个电话|可以帮忙打电话吗|你能不能帮我打一个电话|帮我打个电话可以吗|请帮我打个电话|能不能请你帮忙打个电话
export inform = ^(你)?<祈使语气词>?(<帮助><人称代词>?)?<拨打><数量介词>?<电话><结尾词>?$ => request(intent="inform") <3.0>;


############ answer ############
############ 接听电话: 接听/接听电话/接电话/接一下电话/帮我接电话/帮我接听
############ 暂不支持: 接听张三的电话/接听他的电话
############ 将每个句式写成两个, 是为了防止"接""挂"这种case
###### 人称代词 + 助动词 + 动词 + 数量词 + 名词 + 语气词, <我><想><接听><一个><这个><电话>
## 举例: 接听/接听电话/接电话/接通电话/接一下电话/接听这个电话/接一下这个电话
export answer = ^<问候语>?(<人称代词><助动词>?)?<接><数量介词>?<指代词>?<电话><结尾词>?$ => request(intent="answer") <3.0>;
export answer = ^<问候语>?(<人称代词><助动词>?)?<接听>(<数量介词>?(<指代词>?<电话>))?<结尾词>?$ => request(intent="answer") <3.0>;

###### 助动词 + 人称代词 + 动词 + 数量词 + 名词 + 语气词, <帮><我><接听><一个><这个><电话>
## 举例: 帮我接个电话|可以帮忙接电话吗|你能不能帮我接通一个电话|帮我接听一下电话可以吗|帮我接电话|帮我接听|接这个电话|接听一下这个电话|请帮我接电话
export answer = ^(你)?<祈使语气词>?(<帮助><人称代词>?)?<接听><数量介词>?(<指代词>?<电话>)?<结尾词>?$ => request(intent="answer") <3.0>;
export answer = ^(你)?<祈使语气词>?(<帮助><人称代词>?)?<接><数量介词>?<指代词>?<电话><结尾词>?$ => request(intent="answer") <3.0>;


############ hangup ############
############ 挂断电话: 挂断电话/挂掉电话/挂电话/把电话挂了/
############ 暂不支持: 挂掉张三的电话/挂断他的电话
############ 将每个句式写成两个, 是为了防止"接""挂"这种case
###### 人称代词 + 助动词 + 动词 + 数量词 + 名词 + 语气词, <我><想><挂断><一个><这个><电话>
## 举例: 挂断/挂断电话/挂掉电话/挂电话/挂了电话/挂断一下电话/挂掉这个电话/挂一下这个电话
export hangup = ^<问候语>?(<人称代词><助动词>?)?<挂><数量介词>?<指代词>?<电话><结尾词>?$ => request(intent="hangup") <3.0>;
export hangup = ^<问候语>?(<人称代词><助动词>?)?<挂断>(<数量介词>?(<指代词>?<电话>))?<结尾词>?$ => request(intent="hangup") <3.0>;

###### 助动词 + 人称代词 + 动词 + 数量词 + 名词 + 语气词, <帮><我><挂断><一个><这个><电话>
## 举例: 帮我挂个电话|可以帮忙挂断电话吗|你能不能帮我挂了电话|帮我挂断一下电话可以吗|帮我挂电话|帮我挂断|挂掉这个电话|挂断一下这个电话
export hangup = ^(你)?<祈使语气词>?(<帮助><人称代词>?)?<挂断><数量介词>?(<指代词>?<电话>)?<结尾词>?$ => request(intent="hangup") <3.0>;
export hangup = ^(你)?<祈使语气词>?(<帮助><人称代词>?)?<挂><数量介词>?<指代词>?<电话><结尾词>?$ => request(intent="hangup") <3.0>;

###### <把><这个><电话><挂断><一个>
## 举例: 把电话挂了/把这个电话挂掉/帮忙把电话挂了
export hangup = ^(你)?<祈使语气词>?(<帮助><人称代词>?)?<把><指代词>?<电话><挂断><数量介词>?<结尾词>?$ => request(intent="hangup") <3.0>;


