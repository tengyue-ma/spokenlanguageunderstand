################################
##                            ##
##      author:lengting       ##
##      time:2018-08-22       ##
##      version: v1.1         ##
##                            ##
################################


我	= 我|俺|人家|老娘|本小姐|小爷我;
刚刚	= 刚刚|刚才;
没	= 没|没有|木有;
听清	= 听(清楚?|见|懂|到);
听不清	= 听不(清楚?|见|懂);
什么	= 什么|啥;
说什么	= (说|讲)了?<什么>;

请求前缀	= 你|麻烦|麻烦你|请|请你|能|能不能|能不能够|是否能|能否|你能|你能不能|你能不能够|你是否能|你能否|可以|可不可以|可以不可以|是否可以|可否|你可以|你可不可以|你可以不可以|你是否可以|你可否;

再	= 再|重|重复|重新;
说	= 说|讲;
一遍	= 一(遍|次|下);

句末语气词	= 呗|吧|吗|嘛|么|不;
可否	= 好吗|好么|好嘛|好不|好不好|行吗|行么|行嘛|行不|行不行|可以吗|可以么|可以嘛|可不可以|可以不|如何;
结尾请求	= <句末语气词>|<可否>;


我没听清	= <我>?<刚刚>?<我>?<没><听清>|<我>?<刚刚>?<我>?<听不清>;
你说的话	= 你<刚刚>?说的话?;
你刚刚说什么	= 你?<刚刚>?在?<说什么>|<刚刚>你在?<说什么>;
再说一遍	= 那?<请求前缀>?<再><说>?<一遍><结尾请求>?;
给我听	= 给?<我>听一?听?<结尾请求>?;


export repeat00 = ^<什么>$ => request(intent="repeat", type="tts") <5.0>;

export repeat01 = ^啊?<什么>?<我没听清>(<你说的话>|<你刚刚说什么>)?$ => request(intent="repeat", type="tts") <5.0>;
export repeat02 = ^啊?<什么>?(<你说的话>|<你刚刚说什么>)?<再说一遍><给我听>?$ => request(intent="repeat", type="tts") <5.0>;

export repeat03 = ^啊?<什么>?<我没听清>(<你说的话>|<你刚刚说什么>)?<再说一遍><给我听>?$ => request(intent="repeat", type="tts") <5.0>;
export repeat04 = ^啊?<什么>?<再说一遍><给我听>?<我没听清>(<你说的话>|<你刚刚说什么>)?$ => request(intent="repeat", type="tts") <5.0>;

export repeat05 = ^啊?<什么>?(<你说的话><我没听清>|<你刚刚说什么><我没听清>?)<再说一遍>?<给我听>?$ => request(intent="repeat", type="tts") <5.0>;
export repeat06 = ^啊?<什么>?<再说一遍><给我听>?(<你说的话><我没听清>|<你刚刚说什么><我没听清>?)$ => request(intent="repeat", type="tts") <5.0>;
