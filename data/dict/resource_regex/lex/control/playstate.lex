################################
##                            ##
##      author:lengting       ##
##      time:2018-08-22       ##
##      version: v1.1         ##
##      modify:2019-1-10      ##
##                            ##
################################


#include "../comm/digit.lex"


请求前缀	= 麻烦你?|请你?|麻烦请你?|能|能不能|能不能够|能够不能够|是否能|能否|可以|可不可以|可以不可以|是否可以|可否;
引出介词	= 给|为|替|帮;
第一人称	= 我|咱|俺|孤|朕|寡人|在下|洒家|本座|老夫|哀家|人家|爷|大爷|老娘|本小姐|本大小姐|本人|我们|咱们|我的宝宝|宝宝我;
主观意愿	= 想|要|想要|需要;

语气词	= 吧|吗|嘛|么|呢|啦|不|了吧;
可否	= 好吗|好嘛|好不|好不好|行吗|行嘛|行不|行不行|可以吗|可以嘛|可不可以|可以不|如何|怎么样;
结尾请求	= <语气词>|<可否>;

限定时间	= 先|快|现在|立刻|马上|即刻|稍|稍微|还是;
暂停	= 暂停|停止|停|中止|结束|安静|闭嘴|关闭|关掉|停停|停一停|停停停;
否定	= 别|不要;
播放	= 播|放|播放|唱|念|讲|读|背;
听	= 听|欣赏;

再	= 再|继续|接着|再继续|再接着|再恢复;
不想	= 不是?<主观意愿>?<再>?;
不喜欢	= 不喜欢|讨厌|厌烦|拒绝;

取消	= 取消|结束;
恢复	= 接着|继续|恢复;
之前	= (之前|刚刚|刚才|先前|当前)的?;

时间泛数	= 会|会儿|一会|一会儿|一小会儿|下|一下|片刻;
到	= 到|至;
时间阶段	= 上午|中午|下午|晚上|早晨|凌晨|夜里;
时间点	= (零|一|二|三|四|五|六|七|八|九|十|十一|十二|十三|十四|十五|十六|十七|十八|十九|二十|二十一|二十二|二十三|二十四)点;
时间段	= <一百以内数字>(秒|分钟|小时|天);
时间	= <时间泛数>|<到><时间阶段>?<时间点>|<时间段>;

指示代词	= 这|那|该;
首	= 首|曲|个;
音乐	= 歌|歌曲|音乐|乐曲|曲子;
诗词	= 诗|词|古诗|唐诗|宋词|诗词|古诗词;
故事	= 故事|童话故事; 
播放类别	= <音乐>|<诗词>|<故事>|节目;

过	= 过|等|等过;
等	= 等等|等一等|待会儿?;
暂停复合句式	= <暂停><时间泛数><再><播放>|<暂停><播放>?<时间泛数>?<过><时间泛数><再><播放>?|<暂停><播放>?<时间泛数>?<等><再><播放>?|<暂停><播放>?<时间段>之?后<再><播放>?;
播放复合句式	= <取消><暂停>(开始)?<恢复><之前>?<播放>?的?<播放类别>?|<恢复><之前>?<播放>?的?<播放类别>?不要<暂停>;


######### 暂停 / 停止
export playstate0a = ^(不好听|真难听)?你?<请求前缀>?<播放类别>?<限定时间>?就?<引出介词>?<第一人称>?<主观意愿>?<暂停>你?的?(播放)?<指示代词>?(一|几)?<首>?<播放类别>?<时间>?<结尾请求>?$ => request(intent="playstate", type="stop") <5.4>;
## 主体: 用户
export playstate0b = ^<第一人称>?(现在)?(<不想>|<不喜欢>)<听>你?<引出介词>?<第一人称>?<播放>?<播放类别>?了?<可否>?$ => request(intent="playstate", type="stop") <5.4>;
## 主体: 机器
export playstate0c = ^你?<请求前缀>?<否定><再>?<引出介词>?<第一人称>?<播放><播放类别>?了?<结尾请求>?$ => request(intent="playstate", type="stop") <5.4>;

######### 播放
export playstate1a = ^你?(现在|<等>)?<请求前缀>?<限定时间>?<引出介词>?<第一人称>?(再?开?始?<播放>|<取消><暂停>|<恢复><之前>?<播放>?的?<播放类别>?|<播放复合句式>)了?<结尾请求>?$ => request(intent="playstate", type="play") <5.2>;


######### 特殊处理的句子: 
## 暂停一下再播放
export playstate2a = ^你?<请求前缀>?<限定时间>?<引出介词>?<第一人称>?<暂停复合句式><结尾请求>?$ => request(intent="playstate", type="stop") <5.4>;
## 我不想听了先暂停一会吧
export playstate2b = ^<第一人称>?<不想><听>了?<限定时间>?<暂停><时间泛数>?<结尾请求>?$ => request(intent="playstate", type="stop") <5.4>;
## 我想睡觉了暂停一下 | 我接电话暂停一会
export playstate2c = ^<第一人称>?<主观意愿>?去?(睡觉|接电话|写作业|做作业)了?你?<限定时间>?<暂停><时间泛数>?<结尾请求>?$ => request(intent="playstate", type="stop") <5.4>;
## 不要故事
export playstate2d = ^<第一人称>?<请求前缀>?(<不想>|<不喜欢>)<播放>?<播放类别>了?<可否>?$ => request(intent="playstate", type="stop") <5.4>;
## 别再给我放音乐了我不要听歌
export playstate2e = ^你?<请求前缀>?<否定><再>?<引出介词>?<第一人称>?<播放><播放类别>?了?<第一人称>?<不想><听><播放类别>?了?<可否>?$ => request(intent="playstate", type="stop") <5.4>;


######### 暂停 / 停止 : 扩展
#export playstate2a = <暂停>|<不想><听>|<不喜欢><听>|<否定><再>?<引出介词>?<第一人称>?<播放> => request(intent="playstate", type="stop") <0.06>; 

######### 播放 : 扩展
#export playstate3a = <取消><暂停>|<播放> => request(intent="playstate", type="play") <0.03>;
#export playstate3b = <取消><暂停>.*<播放> => request(intent="playstate", type="play") <0.08>;
