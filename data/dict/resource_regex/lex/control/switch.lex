################################
##                            ##
##      author:lengting       ##
##      time:2018-08-22       ##
##      version: v1.1         ##
##      modify:2019-1-10      ##
##                            ##
################################


#include "../comm/digit.lex"


请求前缀	= 麻烦你?|请你?|麻烦请你?|能|能不能|能不能够|能够不能够|是否能|能否|可以|可不可以|可以不可以|是否可以|可否;
引出介词	= 给|为|替|帮;
第一人称	= 我|咱|俺|孤|朕|寡人|在下|洒家|本座|老夫|哀家|人家|爷|大爷|老娘|本小姐|本大小姐|本人|我们|咱们|我的宝宝|宝宝我;
主观意愿	= 还?是?(想|要|想要);
知道	= 知道|知不知道|知道不知道|了解|了不了解|了解不了解|晓得|晓不晓得|晓得不晓得;
什么	= 什么|啥|啥子;
名字	= 名|名字|名儿|<播放类别>;

语气词	= 吧|吗|嘛|么|呢|不|了|呗;
可否	= 好吗|好么|好嘛|好不|好不好|行吗|行么|行嘛|行不|行不行|可以吗|可以么|可以嘛|可不可以|可以不|如何|怎样|怎么样;
结尾请求	= <语气词>|<可否>;

限定时间	= 先|现在|立刻|马上|即刻|开始;
播放模式	= 继续|循环|重|重复|重新|再|再次|再重复;
播放	= 播|放|播放|唱|讲|念|说|读|背;
播放次数	= <一百以内数字>(次|遍|下);
听	= 听|欣赏|听听;
操作	= (切|换|切换|调|跳|选择)(到|至)?;
操作前缀	= 继续|再|还是;
其他	= (其他|其它|其余|别的|另外的|另一个|另一首|新的)的?;

首	= 首|曲|个;
音乐	= 歌|歌曲|音乐|乐曲|曲子;
诗词	= 诗|词|古诗|唐诗|宋词|诗词|古诗词;
故事	= 故事|童话故事; 
播放类别	= <音乐>|<诗词>|<故事>|节目;

下	= 下|下面|后|后面;
上	= 上|上面|前|前面|之前|先前;
刚刚	= 之前|刚刚|刚才|先前;
上首的指代	= <刚刚><播放>的那一?<首>|<刚刚><听>的那一?<首>|<刚刚>的<播放类别>|<刚刚>的?那一?<首>;
返回	= (回|返回|恢复)(到|至)?;


########### 下一首
## 播放下一首 | 切换下一首
export switch0a = ^你?<请求前缀>?<限定时间>?<引出介词>?<第一人称>?(<播放模式>?<播放><播放次数>?|<操作前缀>?<操作>|<播放模式>|需要)?<下>一?<首><播放类别>?<播放模式>?(<播放>|<听>)?<播放次数>?<结尾请求>?$ => request(intent="switch", type="next") <5.8>;
## 主体-人: 听下一首
export switch0b = ^<第一人称>?(<主观意愿>|<请求前缀>)?<播放模式>?<听><下>一?<首><播放类别>?<结尾请求>?$ => request(intent="switch", type="next") <5.8>;
## 切歌
export switch0c = ^你?<请求前缀>?<限定时间>?<引出介词>?<第一人称>?<主观意愿>?切歌<结尾请求>?$ => request(intent="switch", type="next") <5.8>;
## 歌曲切换
#export switch0d = ^<请求前缀>?<播放类别>切换<结尾请求>?$ => request(intent="switch", type="next") <5.8>;
## 跳过这一首
export switch0e = ^你?<请求前缀>?<限定时间>?<引出介词>?<第一人称>?<主观意愿>?跳过这一?<首><播放类别>?<结尾请求>?$ => request(intent="switch", type="next") <5.8>;
## 换首别的
export switch0f = ^你?<请求前缀>?<限定时间>?<引出介词>?<第一人称>?<主观意愿>?再?换一?<首>?<其他><播放>?<结尾请求>?$ => request(intent="switch", type="next") <5.8>;
## 换一首
export switch0g = ^(真?难听|不好听)?你?<请求前缀>?<限定时间>?<引出介词>?<第一人称>?<主观意愿>?再?换一?(<首>|下)(<播放>|<听>)?<结尾请求>?$ => request(intent="switch", type="next") <5.8>;
## 切换
export switch0h = ^切换$ => request(intent="switch", type="next") <5.8>;
## 下一首是什么
export switch0i = ^你?(<请求前缀>?告?诉?<第一人称>?|<知道>?)<下>一?<首><播放类别>?(是|叫|是叫)<什么><名字>?<语气词>?$ => request(intent="switch", type="next") <5.8>;

########## 上一首
## 播放上一首 | 之前的那首
export switch1a = ^你?<请求前缀>?<限定时间>?<引出介词>?<第一人称>?(<播放模式>?<播放><播放次数>?|<操作>|<返回><播放>?|把|<播放模式>)?(<上>一?<首>|<上首的指代>)<播放类别>?<主观意愿>?<播放模式>?(<播放>|<听>)?<播放次数>?<结尾请求>?$ => request(intent="switch", type="prev") <5.6>;
## 主体-人: 听上一首
export switch1b = ^<第一人称>?(<主观意愿>|<请求前缀>)?<播放模式>?<听><播放次数>?(<上>一?<首>|<上首的指代>)<播放类别>?<结尾请求>?$ => request(intent="switch", type="prev") <5.6>;
## 上一首歌是什么
export switch1c = ^你?(<请求前缀>?告?诉?<第一人称>?|<知道>?)(<上>一?<首>|<上首的指代>)<播放类别>?(是|叫|是叫)<什么><名字>?<语气词>?$ => request(intent="switch", type="prev") <5.6>;


######### 下一首 : 扩展
#export switch2a = <下>一?<首>|切歌|换歌|切换<播放类别>|<播放类别>切换|跳过这一?<首>|换一?<首>?<其他>|换一?<首>|换一?下 => request(intent="switch", type="next") <0.1>;

######### 上一首 : 扩展
#export switch3a = <上>一?<首>|<上首的指代> => request(intent="switch", type="prev") <0.1>;
